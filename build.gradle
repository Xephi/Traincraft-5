buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'idea'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    maven {
        url "http://dvs1.progwml6.com/files/maven"
    }
    maven {
        url 'http://maven.epoxide.xyz'
    }
    maven {
        name = "ic2"
        url = "http://maven.ic2.player.to/"
    }
    ivy {
        name = "Railcraft"
        artifactPattern "http://www.railcraft.info/ivy/com.headlamp-games/Railcraft/[revision]/[module]_" + project.minecraftVersion.toString() + "-[revision]-[classifier].[ext]"
    }
}

version = project.modVersion.toString()
group= "si.meansoft.traincraft"
archivesBaseName = "Traincraft"

minecraft {
    version = project.minecraftVersion.toString() + "-" + project.forgeVersion.toString()
    runDir = "run"
    mappings = project.mappings.toString()
    makeObfSourceJar = false
    useDepAts = true
    replaceIn "Traincraft.java"
    replace "@MODNAME@", project.modName.toString()
    replace "@VERSION@", project.modVersion.toString()
}

dependencies {
    deobfCompile "mezz.jei:jei_" + project.minecraftVersion.toString() + ":" + project.jeiVersion.toString()
    deobfCompile "net.darkhax.tesla:Tesla:1.10.2-" + project.teslaVersion.toString()
    compileOnly "net.industrial-craft:industrialcraft-2:" + project.ic2Version.toString() + ":api"
    compileOnly name: "Railcraft", version: project.railcraftVersion.toString(), classifier: "dev"
}

processResources {
    inputs.property "version", project.modVersion
    inputs.property "mcversion", project.minecraftVersion
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.modVersion, 'mcversion':project.minecraftVersion
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
